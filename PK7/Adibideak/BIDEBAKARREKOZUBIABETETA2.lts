/*
 * PK7.pdf Adibidea
 * Aurrerapena - Bide bakarreko zubiaren eredua zuzentzen
 *
 * Data: 2017-11-27
 */

const N  = 3    // kotxe mota bakoitzeko kopurua
range T  = 0..N // kotxe kontagailuaren mota
range ID = 1..N // kotxeen zenbakiak

KOTXEA = (eskatu->sartu->irten->KOTXEA).

ORDENEAN = ORD[1][1],
ORD[s:ID][i:ID]= ( [s].sartu -> ORD[s%N+1][i]	// sartzeko ordena mantentzen du
				 | [i].irten -> ORD[s][i%N+1]	// irtetzeko ordena mantentzen du
				 ).

||KONBOI = ([ID]:KOTXEA||ORDENEAN).

||KOTXEAK =(gorria:KONBOI||urdina:KONBOI).

//kg zubian dauden gorrien kontagailua
//ku zubian dauden urdinen kontagailuak
//zg zubian sartzeko zain dauden gorrien kontagailua
//zu zubian sartzeko zain dauden urdinen kontagailuak
ZUBIA = ZUBIA[0][0][0][0],
ZUBIA[kg:T][ku:T][zg:T][zu:T] = (						gorria[ID].eskatu->ZUBIA[ kg ][ ku ][zg+1][ zu ]
								|when (ku==0 && zu==0)  gorria[ID].sartu ->ZUBIA[kg+1][ ku ][zg-1][ zu ]
								|						gorria[ID].irten ->ZUBIA[kg-1][ ku ][ zg ][ zu ]
								|						urdina[ID].eskatu->ZUBIA[ kg ][ ku ][ zg ][zu+1]
								|when (kg==0 && zg==0) 	urdina[ID].sartu ->ZUBIA[ kg ][ku+1][ zg ][zu-1]
								|						urdina[ID].irten ->ZUBIA[ kg ][ku-1][ zg ][zu]
								).

||KOTXEAKZUBIAN =(gorria:KONBOI||urdina:KONBOI||ZUBIA).

||ZUBIABETETA = (KOTXEAKZUBIAN)>>{{gorria[ID],urdina[ID]}.irten}.
