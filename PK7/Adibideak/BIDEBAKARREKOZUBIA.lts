/*
 * PK7.pdf Adibidea
 * Bide bakarreko zubia - eredua
 *
 * Data: 2017-11-27
 */

const N  = 3    // kotxe mota bakoitzeko kopurua
range T  = 0..N // kotxe kontagailuaren mota
range ID = 1..N // kotxeen zenbakiak

KOTXEA = (sartu->irten->KOTXEA).

ORDENEAN = ORD[1][1],
ORD[s:ID][i:ID]= ( [s].sartu -> ORD[s%N+1][i]	// sartzeko ordena mantentzen du
				 | [i].irten -> ORD[s][i%N+1]	// irtetzeko ordena mantentzen du
				 ).

||KONBOI = ([ID]:KOTXEA||ORDENEAN).

||KOTXEAK =(gorria:KONBOI||urdina:KONBOI).

ZUBIA = ZUBIA[0][0],  //hasieran hutsik
ZUBIA[kg:T][ku:T] =   //kg: gorrien kontagailua   //ku: urdinena
					(when (ku==0) gorria[ID].sartu -> ZUBIA[kg+1][ ku ]
					|			  gorria[ID].irten -> ZUBIA[kg-1][ ku ]
					|when (kg==0) urdina[ID].sartu -> ZUBIA[ kg ][ku+1]
					|			  urdina[ID].irten -> ZUBIA[ kg ][ku-1]
					).

||KOTXEAKZUBIAN =(gorria:KONBOI||urdina:KONBOI||ZUBIA).
