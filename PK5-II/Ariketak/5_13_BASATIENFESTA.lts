/*
 * PK5.pdf Ariketak
 * 		11. Basatien festa:
 *			- Misiolariak iristean, sukaldariak akatzen ditu,
 *			  hainbat puskatan zatitu, puskak hozkailuan sartu
 *			  eta hortik lapikora, begiratuz beti ea tokia 
 *			  dagoen lapikoan, hozkailuan...
 *
 * Egilea: Jon Led
 * Data: 2017-11-15
 */

const MK 	= 6			// Misiolari kopurua maximoa (Hondartzan)
range MR 	= 0..MK		// Misiolari rangoa (0:inor->MK:beteta)
const BK 	= 3			// Basati kopurua
range BR 	= 1..BK		// Basatien rangoa
range Lock  = 0..1		// Sarraila, biak batera begira ez dezaten (Basatiak
						// begiratu[0] egiten dute eta bitartean sukaldariak
						// botatzen badu gero hartu dezake begiratu[0] egin 
						// ondoren (hau ezin da)
const HT	= 6			// Hozkailuaren tamaina maximoa
range HR	= 0..HT		// Hozkailuaren rangoa (0:inor->MK:beteta)
const LT	= 6			// Lapikoaren tamaina maximoa
range LR	= 0..LT		// Lapikoaren rangoa (0:inor->MK:beteta)
range Aukera= 0..1		// Moztu(0) edo Lapikoan sukaldatu(1)
const Puska = 3			// Zenbat puska moztuko diren


BASATIA 	= (begiratu[k:LR] ->
					if (k>0) then (random[r:1..k] ->hartu[r]->BASATIA)
							  else					 (askatu->BASATIA)).

SUKALDARIA	= (begiratu[m:MR][h:HR][l:LR] -> 
					moztuEdoLapikoanSartu[x:Aukera]->
						if(x==0) then	// Moztu
							if(HT>=h+Puska && m>0) then
								(akatu->botaHozkailura->SUKALDARIA)
							else
								(askatu->SUKALDARIA)
						else
							if (h>0 && h<=LT-l) then
								(random[r:1..h] ->botaLapikora[r]->SUKALDARIA)
							else
								(askatu->SUKALDARIA)).

TXALUPA = (begiratu[m:MR]->if (m<MK) then (random[i:1..MK-m]->iritsi[i]->TXALUPA)
									 else 						(askatu->TXALUPA)).

// [m:MR] Hondartzan dauden misiolari kopurua (hasieran 0)
// [h:HR] Hozkailuan dauden misiolari puska kopurua (hasieran 0)
// [l:LR] Lapikoan dauden misiolari puska kopurua (hasieran 0)
// [s:Lock] Sarraila. Biak batera begiratu akzioa egin ez dezaten
//					  (hasieran askatua edo irikita(0))

LAPIKOA 	 						= LAPIKOA[0][0][0][0],
LAPIKOA[m:MR][h:HR][l:LR][s:Lock]	= (when(m>0)			s.botaHozkailura	  ->LAPIKOA[m-1][ h+Puska ][ l ][0]
									  |when(h>0)			s.botaLapikora[r:1..h]->LAPIKOA[ m ][   h-r   ][l+r][0]			
				  		  			  |when(l>0)		 	b[BR].hartu[r:1..l]	  ->LAPIKOA[ m ][    h    ][l-r][0]
							  		  |when(s==0)		 	b[BR].begiratu[l]     ->LAPIKOA[ m ][    h    ][ l ][1]
									  |when(s==0)		 	s.begiratu[m][h][l]	  ->LAPIKOA[ m ][    h    ][ l ][1]		
									  |when(m<MK)			t.iritsi[r:1..MK-m]	  ->LAPIKOA[m+r][    h    ][ l ][0]
									  |when(s==0)			t.begiratu[m]		  ->LAPIKOA[ m ][    h    ][ l ][1]
									  |						{b[BR],s,t}.askatu	  ->LAPIKOA[ m ][    h    ][ l ][0]
				  					  ).

||BASATIENFESTA = (b[BR]:BASATIA || t:TXALUPA || s:SUKALDARIA || LAPIKOA).
